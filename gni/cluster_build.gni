# Copyright 2026 the V8 project authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/v8_target_cpu.gni")
import("v8.gni")

# Number of source files per cluster for generated files.
torque_cluster_size = 10

# Creates a v8_source_set with cluster build filtering applied to sources.
# When cluster build is enabled, removes files that are #included by cluster
# files. When disabled, removes the cluster files themselves.
#
# Arguments: Same as v8_source_set, with sources being filtered automatically.
template("v8_cluster_source_set") {
  assert(defined(invoker.sources), "sources must be defined")

  _sources = invoker.sources
  _cluster_files = filter_include(_sources, [ "*-cluster*.cc" ])

  if (v8_enable_cluster_build) {
    if (_cluster_files != []) {
      # Note: We deliberately don't pass the cluster files as dependencies
      # (4th argument to exec_script) because when switching between commits
      # that add/remove cluster files, ninja would fail with "file missing".
      # Since cluster files are listed in sources (in BUILD.gn), any addition
      # or removal will trigger a BUILD.gn change which regenerates anyway.
      _excludes = exec_script("//tools/extract_cluster_sources.py",
                              rebase_path(_cluster_files),
                              "list lines")
      _filtered = _sources - _excludes
    } else {
      _filtered = _sources
    }
  } else {
    _filtered = _sources - _cluster_files
  }

  v8_source_set(target_name) {
    sources = _filtered
    forward_variables_from(invoker, "*", [ "sources" ])
  }
}

# Generates cluster files for a list of generated .cc files.
# This is used for torque-generated files where the cluster files
# need to be created during the build.
#
# Arguments:
#   sources: List of generated .cc files to cluster
#   prefix: Prefix for cluster file names (e.g., "torque-csa")
#   output_dir: Directory to write cluster files to
#   deps: Dependencies (e.g., run_torque action)
#
# Outputs:
#   cluster_sources: List of cluster file paths (for use in source sets)
#   In cluster mode: creates an action to generate cluster files
#   In non-cluster mode: creates a no-op group
template("torque_cluster_action") {
  assert(defined(invoker.sources), "sources must be defined")
  assert(defined(invoker.prefix), "prefix must be defined")
  assert(defined(invoker.output_dir), "output_dir must be defined")

  _sources = invoker.sources
  _prefix = invoker.prefix
  _output_dir = invoker.output_dir

  # Handle optional excludes list (files too large to cluster)
  if (defined(invoker.excludes)) {
    _excludes = invoker.excludes
  } else {
    _excludes = []
  }

  # Count source files (excluding large files)
  _num_excludes = 0
  foreach(e, _excludes) {
    _num_excludes += 1
  }
  _num_sources = 0
  foreach(s, _sources) {
    _num_sources += 1
  }
  _num_clustered = _num_sources - _num_excludes

  # Compute cluster file names using exec_script
  _cluster_filenames = exec_script("//tools/compute_cluster_filenames.py",
                                   [
                                     "$_num_clustered",
                                     "$torque_cluster_size",
                                     _prefix,
                                   ],
                                   "list lines")

  # Add directory prefix to get full paths
  _cluster_outputs = []
  foreach(f, _cluster_filenames) {
    _cluster_outputs += [ "$_output_dir/$f" ]
  }

  if (v8_enable_cluster_build) {
    # Compute the strip prefix - this is the path to the output directory
    # relative to the build directory, used to convert source file paths
    # to paths relative to the cluster file location.
    _strip_prefix = rebase_path(_output_dir, root_build_dir) + "/"

    action(target_name) {
      script = "//tools/generate_cluster_files.py"

      outputs = _cluster_outputs

      args = [
        "--output-dir",
        rebase_path(_output_dir, root_build_dir),
        "--prefix",
        _prefix,
        "--cluster-size",
        "$torque_cluster_size",
        "--strip-prefix",
        _strip_prefix,
      ]

      # Add exclude arguments
      foreach(e, _excludes) {
        args += [
          "--exclude",
          rebase_path(e, root_build_dir),
        ]
      }

      args += rebase_path(_sources, root_build_dir)

      forward_variables_from(invoker,
                             [
                               "deps",
                               "public_deps",
                               "visibility",
                             ])
    }
  } else {
    # In non-cluster mode, create a group that just passes through deps
    group(target_name) {
      forward_variables_from(invoker,
                             [
                               "deps",
                               "public_deps",
                               "visibility",
                             ])
    }
  }
}
